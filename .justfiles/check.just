# run go vet
[group('~checks:default')]
vet:
    just _banner ">> running go vet"
    go vet $(go list ./... | grep -v "github.com/dropwhile/icanbringthat/rpc")

# run staticcheck
[group('~checks:default')]
staticcheck:
    just _banner ">> running staticcheck"
    {{ tool_bin }} staticcheck ./...

# run errcheck
[group('~checks:default')]
errcheck:
    just _banner ">> running errcheck"
    {{ tool_bin }} errcheck \
        -ignoretests \
        -exclude .errcheck-excludes.txt \
        ./...

# run errortype
[group('~checks:default')]
errortype:
    just _banner ">> running errortype"
    {{ tool_bin }} errortype ./...

# run nilness
[group('~checks:default')]
nilness:
    just _banner ">> running nilness"
    {{ tool_bin }} nilness ./...

# run ineffassign
[group('~checks:default')]
ineffassign:
    just _banner ">> running ineffassign"
    {{ tool_bin }} ineffassign ./...

# run govulncheck
[group('~checks:default')]
govulncheck:
    just _banner ">> running govulncheck"
    {{ tool_bin }} govulncheck ./...

# run betteralign
[group('~checks:default')]
betteralign:
    just _banner ">> running betteralign"
    {{ tool_bin }} betteralign ./...

# run gosec
[group('~checks:default')]
gosec:
    just _banner ">> running gosec"
    {{ tool_bin }} gosec \
        -quiet \
        -exclude-generated \
        -exclude-dir=cmd/refidgen \
        -exclude-dir=tools ./...

# run nilaway (will have false positives)
[group('~checks:optional')]
nilaway:
    just _banner ">> running nilaway"
    {{ tool_bin }} nilaway -test=false \
        -include-pkgs "github.com/dropwhile/icanbringthat" \
        -exclude-file-docstrings "@generated,Code generated by,Autogenerated by" \
        ./...

# run deadcode (will have false positives)
[group('~checks:optional')]
deadcode:
    just _banner ">> running deadcode"
    {{ tool_bin }} deadcode -test ./...

# run errorlint (will have false positives)
[group('~checks:optional')]
errorlint:
    just _banner ">> running errorlint"
    {{ tool_bin }} go-errorlint ./...

# run all ~checks:default checkers
[group('checks')]
check: vet staticcheck errcheck errortype nilness ineffassign govulncheck betteralign gosec

# run all ~checks:optional checkers
[group('checks')]
check-optional: nilaway deadcode errorlint
